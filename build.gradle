  
plugins {
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

def projectName = 'Mediscreen'

subprojects {

    group = 'com.abernathyclinic'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.9.RELEASE")
        }
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
	
	// CUSTOM DOCKER TASKS
	task buildDocker (type: Exec) {
		dependsOn "bootJar" 
		group = "Docker"
		description = "Build the docker image"
		commandLine "docker", "build", "--build-arg", "JAR_FILE=build/libs/*.jar", "-t", project.name.toLowerCase(), "."
	}

	task cleanDocker (type: Exec) {
		group = "Docker"
		ignoreExitValue = "true"
		description = "Delete the docker container"
		commandLine "docker", "rmi", "-f", project.name.toLowerCase()
	}
}

task runBackDocker (type: Exec) {
	dependsOn "mediscreen-patient:buildDocker", "mediscreen-note:buildDocker"
	group = "Docker"
	description = "Start all project micro services"
	commandLine "docker" ,"compose", "-p", "mediscreen", "-f", "docker/docker-compose-back.yml", "up", "-d"
}

task stopBackDocker (type: Exec) {
	group = "Docker"
	description = "Stop all project micro services"
	commandLine "docker" ,"compose", "-p", "mediscreen", "-f", "docker/docker-compose-back.yml", "down"
	finalizedBy "mediscreen-patient:cleanDocker", "mediscreen-note:cleanDocker"
}

task runDocker (type: Exec) {
	dependsOn "mediscreen-patient:buildDocker", "mediscreen-note:buildDocker"
	group = "Docker"
	description = "Start all project micro services"
	commandLine "docker" ,"compose", "-p", "mediscreen", "-f", "docker/docker-compose.yml", "up", "-d"
}

task stopDocker (type: Exec) {
	group = "Docker"
	description = "Stop all project micro services"
	commandLine "docker" ,"compose", "-p", "mediscreen", "-f", "docker/docker-compose.yml", "down"
	finalizedBy "mediscreen-patient:cleanDocker", "mediscreen-note:cleanDocker"
}